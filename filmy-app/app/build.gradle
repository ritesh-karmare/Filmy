plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
}

ext.versionMajor = 5
ext.versionMinor = 1
ext.versionPatch = 2
ext.versionClassifier = "dev"
ext.isDevelopment = false
ext.minimumSdkVersion = 21
ext.targetSdkVersion = 33

android {

    compileSdkVersion 33

    defaultConfig {
        applicationId "rk.entertainment.filmy"

        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion

        versionCode generateVersionCode()
        versionName generateVersionName()

        multiDexEnabled true

        buildConfigField 'String', "API_KEY", API_KEY
        buildConfigField 'String', "YOUTUBE_API_KEY",YOUTUBE_API_KEY

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support dependencies
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // OkHttp, Retrofit and Gson networking libraries
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.9.0"
    implementation("com.squareup.retrofit2:retrofit:2.9.0") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.google.code.gson:gson:2.9.1"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    kapt 'com.github.bumptech.glide:compiler:4.13.2'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.6.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-perf'

    // Youtube Player
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    // Coroutines, ViewModel, LiveData
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.0'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.0"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.0'

    // hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"

}

//  Generate Version Code
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

//Generate Version Name
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && ext.isDevelopment) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
