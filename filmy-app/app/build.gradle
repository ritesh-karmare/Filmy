apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

ext.versionMajor = 3
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "dev"
ext.isDevelopment = false
ext.minimumSdkVersion = 16
ext.targetSdkVersion = 28

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "rk.entertainment.filmy"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        multiDexEnabled true
    }

    buildTypes {
        debug {
            buildConfigField 'String', "API_KEY", Filmy_ApiKey
            buildConfigField 'String', "YOUTUBE_API_KEY", Filmy_Youtube_ApiKey

        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "API_KEY", Filmy_ApiKey
            buildConfigField 'String', "YOUTUBE_API_KEY", Filmy_Youtube_ApiKey
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {

    SUPPORT_LIBRARY_VERSION = '28.0.0'
    SUPPORT_CONSTRAINT_LAYOUT = "1.1.3"

    OKHTTP_VERSION = '4.2.0'
    RETROFIT_VERSION = '2.6.1'
    GSON_VERSION = '2.8.5'

    FIREBASE_PERFORMANCE_MONITORING = '19.0.0'
    FIREBASE_ANALYTICS = '17.2.0'

    RX_JAVA_VERSION = '2.2.12'
    RX_ANDROID_VERSION = '2.1.1'

    BUTTERKNIFE_VERSION = '10.1.0'
    GLIDE_VERSION = '4.10.0'

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support dependencies
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // OkHttp, Retrofit and Gson networking libraries
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    implementation("com.squareup.retrofit2:retrofit:$RETROFIT_VERSION") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.google.code.gson:gson:$GSON_VERSION"

    // Butterknife
    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // Glide
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    // RX Java/Android
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID_VERSION"

    // Firebase
    implementation "com.google.firebase:firebase-perf:$FIREBASE_PERFORMANCE_MONITORING"
    implementation "com.google.firebase:firebase-core:$FIREBASE_ANALYTICS"

    // Youtube Player
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
}

apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

//  Generate Version Code
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

//Generate Version Name
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && ext.isDevelopment) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
