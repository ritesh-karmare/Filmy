apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

ext.versionMajor = 2
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "dev"
ext.isDevelopment = false
ext.minimumSdkVersion = 16
ext.targetSdkVersion = 27

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "rk.entertainment.filmy"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            ext.enableCrashlytics = false
            buildConfigField 'String', "API_KEY", Filmy_ApiKey
            buildConfigField 'String', "YOUTUBE_API_KEY", Filmy_Youtube_ApiKey

        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField 'String', "API_KEY", Filmy_ApiKey
            buildConfigField 'String', "YOUTUBE_API_KEY", Filmy_Youtube_ApiKey
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {

    SUPPORT_LIBRARY_VERSION = '27.1.1'
    SUPPORT_CONSTRAINT_LAYOUT = "1.1.3"

    OKHTTP_VERSION = '3.11.0'
    RETROFIT_VERSION = '2.4.0'
    GSON_VERSION = '2.8.5'

    FIREBASE_PERFORMANCE_MONITORING = '16.2.0'
    FIREBASE_ANALYTICS = '16.0.4'

    RX_JAVA_VERSION = '2.2.0'
    RX_ANDROID_VERSION = '2.0.2'

    TIMBER_VERSION = '4.7.1'

    BUTTERKNIFE_VERSION = '8.8.1'
    GLIDE_VERSION = '4.7.1'

    JUNIT_VERSION = '4.12'
    RUNNER_VERSION = '1.0.2'
    ESPRESSO_VERSION = '3.0.2'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support dependencies
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:cardview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$SUPPORT_CONSTRAINT_LAYOUT"

    // OkHttp, Retrofit and Gson networking libraries
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"
    implementation "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    implementation("com.squareup.retrofit2:retrofit:$RETROFIT_VERSION") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"
    implementation "com.google.code.gson:gson:$GSON_VERSION"

    // Butterknife
    implementation "com.jakewharton:butterknife:$BUTTERKNIFE_VERSION"
    annotationProcessor "com.jakewharton:butterknife-compiler:$BUTTERKNIFE_VERSION"

    // Glide
    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    annotationProcessor "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    // RX Java/Android
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA_VERSION"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID_VERSION"

    // Timber
    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

    // Firebase
    implementation "com.google.firebase:firebase-perf:$FIREBASE_PERFORMANCE_MONITORING"
    implementation "com.google.firebase:firebase-core:$FIREBASE_ANALYTICS"

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.5@aar') {
        transitive = true
    }

    // Youtube Player
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    // Local Unit tests
    testImplementation "junit:junit:$JUNIT_VERSION"

    // Espresso Unit testing
    androidTestImplementation "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

//  Generate Version Code
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

//Generate Version Name
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && ext.isDevelopment) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
