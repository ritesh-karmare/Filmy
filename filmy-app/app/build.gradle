apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

ext.versionMajor = 4
ext.versionMinor = 0
ext.versionPatch = 0
ext.versionClassifier = "dev"
ext.isDevelopment = false
ext.minimumSdkVersion = 16
ext.targetSdkVersion = 28

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {

    compileSdkVersion 28

    defaultConfig {
        applicationId "rk.entertainment.filmy"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        multiDexEnabled true

        buildConfigField 'String', "API_KEY", apikeyProperties['Filmy_ApiKey']
        buildConfigField 'String', "YOUTUBE_API_KEY", apikeyProperties['Filmy_Youtube_ApiKey']

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    viewBinding {
        enabled = true
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support dependencies
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    //implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.0"

    // OkHttp, Retrofit and Gson networking libraries
    implementation "com.squareup.okhttp3:okhttp:4.2.1"
    implementation "com.squareup.okhttp3:logging-interceptor:4.2.1"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.8.1"
    implementation("com.squareup.retrofit2:retrofit:2.8.1") { exclude module: 'okhttp' }
    implementation "com.squareup.retrofit2:converter-gson:2.8.1"
    implementation "com.google.code.gson:gson:2.8.6"

    // Butterknife
    implementation "com.jakewharton:butterknife:10.1.0"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    kapt "com.jakewharton:butterknife-compiler:10.1.0"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.6.0')
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-perf'

    // Youtube Player
    implementation files('libs/YouTubeAndroidPlayerApi.jar')

    implementation 'com.android.support:multidex:1.0.3'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.4.30'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

}

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

//  Generate Version Code
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

//Generate Version Name
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null && ext.isDevelopment) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName
}
